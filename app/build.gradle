plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.simplyteam.simplybackup"
        minSdk 26
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.simplyteam.simplybackup.HiltTestRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        signingConfig signingConfigs.debug
    }

    buildTypes {
        debug {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())

            buildConfigField "String", "NEXTCLOUD_HOST", properties.getProperty('NEXTCLOUD_HOST')
            buildConfigField "String", "NEXTCLOUD_USERNAME", properties.getProperty('NEXTCLOUD_USERNAME')
            buildConfigField "String", "NEXTCLOUD_PASSWORD", properties.getProperty('NEXTCLOUD_PASSWORD')

            buildConfigField "String", "SFTP_HOST", properties.getProperty('SFTP_HOST')
            buildConfigField "String", "SFTP_USERNAME", properties.getProperty('SFTP_USERNAME')
            buildConfigField "String", "SFTP_PASSWORD", properties.getProperty('SFTP_PASSWORD')
            buildConfigField "String", "SFTP_REMOTEPATH", properties.getProperty('SFTP_REMOTEPATH')

            buildConfigField "String", "GOOGLE_DRIVE_USER", properties.getProperty('GOOGLE_DRIVE_USER')
            buildConfigField "String", "GOOGLE_DRIVE_EDITUSER", properties.getProperty('GOOGLE_DRIVE_EDITUSER')
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'

        if (project.findProperty("myapp.enableComposeCompilerReports") == "true") {
            freeCompilerArgs += [
                    "-P",
                    "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                            project.buildDir.absolutePath + "/compose_metrics"
            ]
            freeCompilerArgs += [
                    "-P",
                    "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                            project.buildDir.absolutePath + "/compose_metrics"
            ]
        }
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    configurations.all {
        resolutionStrategy.force 'junit:junit:4.13.2'
    }
    kapt {
        correctErrorTypes true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'androidx.activity:activity-compose:1.4.0'
    implementation "com.google.accompanist:accompanist-navigation-animation:0.24.6-alpha"

    //Dagger - Hilt
    implementation 'com.google.dagger:hilt-android:2.41'
    kapt 'com.google.dagger:hilt-compiler:2.41'

    //Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'

    //Logging
    implementation 'com.jakewharton.timber:timber:5.0.1'

    //Room
    implementation "androidx.room:room-ktx:2.4.2"
    kapt "androidx.room:room-compiler:2.4.2"

    //Moshi
    implementation "com.squareup.moshi:moshi:1.13.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.13.0"

    //Zip
    implementation "net.lingala.zip4j:zip4j:2.10.0"

    //Nextcloud
    implementation('com.github.nextcloud:android-library:-SNAPSHOT'){
        exclude group: 'junit', module: 'junit'
    }
    implementation "commons-httpclient:commons-httpclient:3.1@jar"

    //SFTP
    implementation "com.jcraft:jsch:0.1.55"

    //Google Sign In
    implementation 'com.google.android.gms:play-services-auth:20.1.0'

    //Drive
    implementation('com.google.api-client:google-api-client-android:1.33.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude module: 'guava-jdk5'
    }
    implementation('com.google.apis:google-api-services-drive:v3-rev136-1.25.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude module: 'guava-jdk5'
    }

    //Instrumentation Testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.2.0-alpha07"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation("androidx.compose.ui:ui-test-manifest:$compose_version")
    androidTestImplementation  'com.google.dagger:hilt-android-testing:2.41'
    kaptAndroidTest 'com.google.dagger:hilt-compiler:2.41'
    androidTestImplementation "androidx.work:work-testing:2.7.1"

    //Unit testing
    testImplementation "junit:junit:4.13.2"

    //Leak Canary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.8.1'

    //WorkManager
    implementation "androidx.work:work-runtime-ktx:2.7.1"
    implementation 'androidx.hilt:hilt-work:1.0.0'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'

}